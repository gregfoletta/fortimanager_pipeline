#!/usr/bin/env perl

use strict;
use warnings;
use 5.010;
use Pod::Usage;
use Getopt::Long;
use Term::ANSIColor;
use Carp;
use Mojo::UserAgent;
use JSON::PP;
use URI;
use Hash::Flatten qw(:all);
use Data::Dumper;


my %args;
GetOptions(
    \%args,
    'fmg=s',
    'username=s',
    'password=s',
    'insecure|k',
    'help' => sub { pod2usage(1) }
) or pod2usage(2);

=pod

=encoding UTF-8

=head1 NAME

fmg_meta - register and assign meta fields and meta variables on a FortManager

=head1 VERSION

version .1 (BETA)

=head1 SYNOPSIS

    # Register the definitions and assignments against an FMG 
    ./fmg_meta --fmg https://fmg.i.foletta.xyz meta.json

    # Insecure if the certificate is not trusted
    ./fmg_meta --insecure --fmg https://fmg.i.foletta.xyz meta.json

    # API user/pass can be specified as ENV vars
    export FMG_API_USER=api_user
    export FMG_API_PASS=c0mpl3xp4ssw0rd
    ./fmg_meta --fmg https://fmg.i.foletta.xyz meta.json

    # Or on the command line (--username and --password work as well)
    ./fmg_meta --u api_user -p c0mpl3xp4ssw0rd --fmg https://fmg.i.foletta.xyz meta.json

=head1 OPTIONS

=over

=item -f|--fmg I<uri> - the URI of the FortiManager API

=item -u|--username I<user> - the FortiManager API username

=item -p|--password I<pass> - the FortiManager API password

=item -k|--insecure - don't verify certificate 

=item -h|--help - print usage information

=back

=head1 BETA NOTES

Please note that this script is currently in beta. Two main things to take into account:

=over

=item The schema of the JSON file you use to define/apply the meta fields/variables may change at any time.

=item It only supports defining and assigning meta fields, not meta variables (which are new in 7.2).

=back

=head1 REQUIREMENTS

You'll need the following modules, preferably installed using the more modern L<cpanminus|https://metacpan.org/pod/App::cpanminus>:

    sh$ cpanm Mojo::UserAgent

or the old CPAN client:

    sh$ cpan Mojo::UserAgent

=head1 JSON DEFINITION

A an example C<meta.json> file has been included in this repository.

=head1 AUTHENTICATION

Once you have defined you API user on the FortiManager, the script will search for the credentials in three places:

=over 

=item In $HOME/.ftnt/fmg_api formatted as <username>:<password>

=over

=item  Lines beginning with '#' are skipped

=back

=item In the environment variabes C<FMG_API_USER> and C<FMG_API_PASS>

=item In the command line arguments C<-u|--username> and C<-p|--password>.

=back

If the credentials are available in multiple places, local dotfile beats environment variable beats commandline.

=cut


### Log Helpers###

sub log_print {
    print STDERR colored('- ', 'bold green');
    say STDERR join ' ', @_;
}

sub log_change {
    print STDERR colored('+ ', 'bold green');
    say STDERR colored(join(' ', @_), 'green');
}

sub log_error {
    print STDERR colored('- ', 'bold red');
    say STDERR colored('- '.join(' ', @_), 'red');
    exit(1);
}

sub log_warning {
    print STDERR colored('- ', 'bold yellow');
    say STDERR colored(join(' ', @_), 'yellow');
}

# Pull the host from the command line or an env
my $fmg = $args{fmg} // $ENV{FMG_API_URI};

log_error "No --fmg specified" unless $fmg;

# JSON file is the last argument
my $json_file = shift or log_error "No JSON file specififed";
my $fmg_data = get_metavar_json($json_file);

log_print "Retreived metavars from JSON file $json_file";
log_print "Authenticating to FortiManager";

# Get our API access token
my $api_json_res = fmg_api_call(
    fmg => $fmg,
    method => 'exec',
    insecure => $args{insecure},
    url => '/sys/login/user',
    data => {
        user => $ENV{FMG_API_USER},
        passwd  => $ENV{FMG_API_PASS},
    }
);

my $session_token = $api_json_res->{session} or log_error "No session token received";
log_print "Authenticated: ".substr($session_token, 0, 16)."...";

# Create a little anonymous subroutine that wraps up our user agent
# We can pass this amogst the functions and all they have to add is
# the 'url => ' and 'data =>' sections
my $api_ua = sub {
    return fmg_api_call(
        fmg => $fmg,
        session => $session_token,
        method => 'get',
        insecure => $args{insecure},
        @_
    )
};


apply_config($fmg_data, $api_ua);


sub apply_config {
    my ($fmg_data, $api_ua) = @_;

    for my $adom (@{ $fmg_data->{adoms} }) {
        create_adoms($adom, $api_ua);
        create_adom_metavars($adom, $api_ua);
        create_devices($adom, $api_ua);
        create_device_metavars($adom, $api_ua);
    }
}


sub create_adoms {
    my ($adom, $api_ua) = @_;
    my $res;

    # First step is to get the current list of configured ADOMs
    log_print "Retrieving current ADOMs";
    $res = $api_ua->(url => "/dvmdb/adom"); 

    # Filter out non-user created ADOMs and create a hash
    my %current_adoms = 
        map { $_->{name} => 1 }
        grep { $_->{create_time} != 0 }
        @{ $res->{result}[0]{data} };

    # Manually add root
    $current_adoms{root} = 1; 


    my $update = !!$current_adoms{ $adom->{name} };
    log_change $update ? "Updating" : "Creating", "$adom->{name} ADOM";

    $res = $api_ua->(
        method => $update ? 'update' : 'add',
        url => "/dvmdb/adom",
        data => [{
            name => $adom->{name},
            desc => $adom->{description},
            os_ver => $adom->{fos_version}[0],
            mr => $adom->{fos_version}[1],
        }]
    ); 
}
    

sub create_adom_metavars {
    my ($adom, $api_ua) = @_;
    my $res;

    # Get the current metavars
    $res = $api_ua->(url => "/pm/config/adom/$adom->{name}/obj/fmg/variable");

    my %current_metavars = 
        map { $_->{name} => $_->{value} }
        @{ $res->{result}[0]{data} };

    my $flat_metavars = flatten $adom->{variables}, { ArrayDelimiter => '.' };

    METAVAR:
    while (my ($mv_name, $mv_value) = each %{ $flat_metavars }) {
        my $update = 0;
        # Could be undefined
        my $current_mv_value = $current_metavars{ $mv_name };

        if (!defined $current_mv_value) {
            log_change "Creating ADOM metavar '$mv_name' ($mv_value)";
        } elsif ($current_mv_value eq $mv_value) {
            log_print "ADOM metavar '$mv_name' unchanged ($mv_value), skipping";
            next METAVAR;
        } else {
            $update = 1;
            log_change "Updating ADOM metavar '$mv_name' ($current_mv_value -> $mv_value)";
        }

        $res = $api_ua->(
            method => $update ? 'update' : 'add',
            url => "/pm/config/adom/$adom->{name}/obj/fmg/variable",
            data => [{ 
                name => $mv_name,
                value => $mv_value
            }]
        ); 
    }
}


sub create_devices {
    my ($adom, $api_ua) = @_;
    my $res;

    # Get the current devices in the ADOM and create mappings
    $res = $api_ua->(url => "/dvmdb/adom/$adom->{name}/device"); 

    my %current_devices = 
        map { $_->{name} => 1 }
        @{ $res->{result}[0]{data} };


    DEVICE:
    for my $device (@{ $adom->{devices} }) {
        if ($current_devices{ $device->{name} }) {
            log_print "Device '$device->{name}' already added, skipping";
            next DEVICE;
        }

        log_change "Creating model device '$device->{name}' in ADOM $adom->{name}";

        $res = $api_ua->(
            method => 'exec',
            url => '/dvm/cmd/add/device',
            data => { 
                adom => $adom->{name},
                device => {
                    name => $device->{name},
                    desc => $device->{description},
                    sn => $device->{serial},
                    'device action' => 'add_model',
                    mgmt_mode => 'fmg',
                    os_type => 'fos',
                    os_ver => $device->{fos_version}[0],
                    mr => $device->{fos_version}[1],
                },
                flags => ['none']
            }
        ); 
    }
}


sub create_device_metavars {
    my ($adom, $api_ua) = @_;
    my $res;

    # Get the per-device metavars for all devices
    $res = $api_ua->(url => "/pm/config/adom/$adom->{name}/obj/fmg/variable");

    # Determine the currently created variables at a {device,vdom,variable} level
    my %current_metavars; 
    for my $metavar (@{ $res->{result}[0]{data} }) {
        # The metavar may have a default value (i.e. ADOM specific), but no dynamic value.
        my $name = $metavar->{name};
        # Use '(default)' as it's an illegal device name
        $current_metavars{ $name }{"(default)"} = $metavar->{value};

        # Add the dynamic values if they exist 
        if (defined $metavar->{dynamic_mapping}) {
            for my $dyn_mapping(@{ $metavar->{dynamic_mapping} } ) {
                my ($device, $vdom) = @{ $dyn_mapping->{_scope}[0] }{ qw(name vdom) };

                $current_metavars{ $name }{ $device }{ $vdom } = $dyn_mapping->{value};
            }
        }
    }

    DEVICE:
    for my $device (@{ $adom->{devices} }) {
        my $flat_metavars = flatten $device->{variables}, { ArrayDelimiter => '.' };
        METAVAR:
        while (my ($mv_name, $mv_value) = each %{ $flat_metavars }) {
            my $update = 0;
            # This could be undefined
            my $current_mv_value = $current_metavars{ $mv_name }{ $device->{name} }{ 'global' };

            print Dumper \%current_metavars;
            print $mv_name;

            # If the metvar isn't created at all, we need to create it before we can make device
            # specific metavar calls. We set a blank default value.
            if (!defined $current_metavars{ $mv_name }) {
                log_warning "Metavar '$mv_name' doesn't exist, creating with an empty default value";

                $res = $api_ua->(
                    method => 'add',
                    url => "/pm/config/adom/$adom->{name}/obj/fmg/variable",
                    data => [{ 
                        name => $mv_name,
                        value => ''
                    }]
                ); 
            }

            if (!defined $current_mv_value) {
                log_change "Creating metavar '$mv_name' for device '$device->{name}' in global ($mv_value)";
            } elsif ($current_mv_value eq $mv_value) {
                log_print "Device metavar '$mv_name' for device '$device->{name}' in global unchanged ($mv_value), skipping";
                next METAVAR;
            } else {
                $update = 1;
                log_change "Updating metavar '$mv_name' for device '$device->{name}' in global ($current_mv_value -> $mv_value)";
            }

            $res = $api_ua->(
                method => $update ? 'update' : 'add',
                url => "/pm/config/adom/$adom->{name}/obj/fmg/variable/$mv_name/dynamic_mapping",
                data => [{
                    value => $mv_value,
                    _scope => [{ 
                        vdom => 'global',
                        name => $device->{name}
                    }]
                }]
            );
        }
    }
}
            

sub get_metavar_json {
    my ($json_file) = @_;
    my $json;

    open(my $fh, "<:encoding(UTF-8)", $json_file) or croak "Could not open JSON file '$json_file'";

    {
        local $/;
        $json = <$fh>;
        close $fh;
    }

    return decode_json($json);
}

sub fmg_api_call {
    my (%a) = @_;

    # Mandatory arguments
    croak "No 'fmg' host/IP address specified" unless $a{fmg};

    # Turn the URI into a URI object
    my $fmg_uri = URI->new($a{fmg}) or log_error "Invalid URI: '$a{fmg}'";
    # Add the path on to the end and remove any queries
    $fmg_uri->path('/jsonrpc');
    $fmg_uri->query(undef);

    #Set our defaults
    $a{method} //= 'get';
    $a{insecure} //= 0;
    $a{session} //= '';
    $a{id} //= 1;

    my $ua = Mojo::UserAgent->new;
    $ua->insecure($args{insecure});

    my $res = $ua->post(
        $fmg_uri->as_string => json => {
            id => $a{id},
            session => $a{session},
            method => $a{method},
            params => [{
                url => "$a{url}",
                data => defined $a{data} ? $a{data} : {}
            }]
        }
    )->result;

    my $json = $res->json;

    log_error "HTTP error: ". $res->body if (!defined $json);

    my $api_status = $json->{result}[0]{status};

    if ($res->is_error or $json->{error} or $api_status->{code} != 0) {
        # Find the error message somewhere in the JSON
        my ($error_message) = grep { defined } (
                @{ $json }{ qw( oauth error_message error_description ) },
                $api_status->{message}
        );

        log_error "API error: $error_message";
    }

    return $json;
}

